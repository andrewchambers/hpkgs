(use ../prelude)
(import ../core)

(defsrc perl-src
  :url
  "https://www.cpan.org/src/5.0/perl-5.30.2.tar.gz"
  :hash
  "sha256:66db7df8a91979eb576fac91743644da878244cf8ee152f02cd6f5cd7a731689")

(defpkg perl
  :builder
  (fn []
    (os/setenv "PATH"
               (string "/bin:"
                       (join-pkg-paths ":" "/bin"
                                       [core/awk core/coreutils core/dash
                                        core/gcc core/grep core/make
                                        core/sed])))
    (os/setenv "CFLAGS" *default-cflags*)
    (os/setenv "LDFLAGS" *default-ldflags*)
    (unpack-src perl-src)
    # patching path to pwd
    (os/chmod "dist/PathTools/Cwd.pm" 8r644)
    (rewrite "dist/PathTools/Cwd.pm"
             |(string/replace-all "'/bin/pwd'"
                                  (string "'"
                                          (core/coreutils :path) "/bin/pwd"
                                          "'")
                                  $))
    (os/chmod "dist/PathTools/Cwd.pm" 8r444)
    # other assumed bits
    (core/link-/bin/sh)
    (sh/$ ./configure.gnu
          -Dprefix= ^ (dyn :pkg-out)
          -Dcc=gcc)
    (sh/$ make -j (dyn :parallelism))
    (sh/$ make install)))
